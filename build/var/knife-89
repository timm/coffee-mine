#!/usr/bin/env coffee
# Copyright (c) 2011,2012 Tim Menzies, MIT License
# 
#  #    #  #    #     #    ######  ######
#  #   #   ##   #     #    #       #
#  ####    # #  #     #    #####   #####
#  #  #    #  # #     #    #       #
#  #   #   #   ##     #    #       #
#  #    #  #    #     #    #       ######
# 
# Version ....... : knife-89
# Built ......... : Mon Jan  2 18:10:43 EST 2012
# License ....... : see below.
# Installation .. : see below.
# Report bugs ... : https://github.com/timm/coffee-mine/issues
# For doco ...... : read http://coffee.unbox.org around Mon Jan  2 18:10:43 EST 2012
 
#--| lib/globals.coffee |-------------------------------------------------------------
 
pow  = Math.pow
e    = Math.E
pi   = Math.PI
sqrt = Math.sqrt
ln   = Math.log
show = console.log

inf  = 1e+32
ninf = -1 * inf

d2 = (n) -> Math.round(n*100)/100

d3 = (n) -> Math.round(n*1000)/1000

d4 = (n) -> Math.round(n*10000)/10000

nummat = (number, decimals, dec=".", sep=",") ->
  # from http://goo.gl/2vcMH
  number = (number + '').replace(/[^0-9+\-Ee.]/g, '')
  n      = if isFinite(+number) then +number else 0
  prec   = if isFinite(+decimals) then Math.abs(decimals) else 0
  s      = ''
  toFixedFix =  (n, prec) ->
    k = Math.pow(10, prec)
    '' + Math.round(n * k) / k
  s = if prec then toFixedFix(n, prec) else '' + Math.round(n)
  s = s.split('.')
  if (s[0].length > 3)
     s[0] = s[0].replace(/\B(?=(?:\d{3})+(?!\d))/g, sep)
  if (s[1]  || '').length < prec
      s[1] = s[1] || ''
      s[1] += new Array(prec - s[1].length + 1).join('0')
   s.join(dec)
 
#--| lib/rand.coffee |-------------------------------------------------------------
 
class Rand
  # Knuth and Lewis' improvements to Park and Miller's LCPRNG
  # if created without a seed, uses current time as seed
  constructor: (@seed) ->
    @multiplier = 1664525
    @modulo     = 4294967296 # 2**32-1;
    @offset     = 1013904223
    unless @seed? && 0 <= seed < @modulo
      @seed = (new Date().valueOf() * new Date().getMilliseconds()) % @modulo

  seed: (seed)      -> @seed = seed
  randn:            -> @seed = (@multiplier*@seed + @offset) % @modulo
  randf:            -> @randn() / @modulo
  rand:  (n)        -> Math.floor(@randf() * n)
  rand2: (min, max) -> min + @rand(max-min)

R = new Rand

shuffle = (l,r = R) ->
  n = l.length
  for i in [n-1..1]
    j = r.rand2(0,i)
    [ l[i],  l[j] ] = [ l[j], l[i] ]
  l

normal = (m,s,r = R) ->
  boxMuller = () ->
    w=1
    while w >= 1
      x1 = 2.0 * r.randf() - 1
      x2 = 2.0 * r.randf() - 1
      w  = x1*x1 + x2*x2
    w = sqrt((-2.0 * ln(w))/w)
    x1 * w
  m + boxMuller() * s

any = (l,r=R) ->
  l[r.rand2(0,l.length)]
 
#--| lib/getline.coffee |-------------------------------------------------------------
 
fs = require "fs"

errorWrapper = (action) ->
  (err, args...) ->
    if err then throw err
    action args...

ifFileExists = (filename, action) ->
   fs.stat filename, errorWrapper (stat) ->
     if stat.isFile() then action()

getFileAsLines = (filename, action) ->
   ifFileExists filename, ->
     fs.readFile filename, "utf8",
       errorWrapper (content) ->
         action content.split "\n"
 
#--| lib/stats.coffee |-------------------------------------------------------------
 
class Distribution
  constructor: (l) -> @adds(l)
  adds:(l) -> @add x for x in l if l

class Bins extends Distribution
  constructor:(l) ->
    @n = 0
    @symbols = 0
    @symbolTable = {}
    @all = {}
    @most = -1
    @mode = null
    super l

  add:(x) ->
    @n = @n + 1
    now = @all[x] = (@all[x] or= 0) + 1
    if now > @most
      @mode = x
      @most = now
    if now is 1
      @symbols += 1
      @symbolTable[x] = @symbols
    x

  p:(x) -> (@all[x] ? 0)/@n
  prob:(x,m,prior) -> ((@all[x] ? 0) + m*prior)/(@n + m)

class Normal extends Distribution
  constructor:(l) ->
    @max = -1000000000
    @min = 1000000000
    @n   = 0
    @sum = 0
    @sumSq = 0
    super l

  prob:(x,m,prior) -> @p x
  add:(x) ->
    @n     = @n + 1
    @sum   = @sum + x
    @sumSq = @sumSq + x*x
    @min   = x if x < @min
    @max   = x if x > @max
    @_mean = @_stdev = null
    x

  mean:  -> @_mean or= @sum/@n
  stdev: ->
    @_stdev or= sqrt((@sumSq-((@sum*@sum)/@n))/(@n-1))

  p:(x) ->
    s = @stdev()
    1/(sqrt(2*pi)*s) * (pow e, (-1*(pow x-@mean(),2)/(2*s*s)))
 
#--| lib/col.coffee |-------------------------------------------------------------
 
class Col
  constructor:(s,i) ->
    @name   = s
    @pos    = i
    @f      = []
    @goalp  = (s.search /\!/) >= 0
    @missing = 0
    @all     = @counter()

  at:(n)       -> @f[n] = @f[n] or= @counter()
  add:(x,n)    ->
    if @missingp x
      @missing += 1
    else
      @all.add(x)
      @at(n).add(x)

  missingp:(s) -> s is "?"
  prob:(x,n,m,prior) -> @at(n).prob(x,m,prior)

class Num extends Col
  counter: () -> new Normal
  prep:(x) -> if @missingp x then x else +x

class Sym extends Col
  counter: () -> new Bins
  prep:(x) -> x



 
#--| knife.coffee |-------------------------------------------------------------
 
class Row
  constructor:(cells,reader) ->
    @cells = cells
    @klass = @isa(reader.goals)
    h.add @cells[h.pos],@klass for h in reader.head

  isa:  (goals) -> @klass or= @isa0 goals
  isa0: (goals) -> (@cells[g.pos] for g in goals).join("-")

class Reader
  constructor: () ->
    @n     = -1    # #attributes    seen so far
    @head  = []    # store data for each column
    @data  = false # are we in the data section?
    @goals = []
    @ins   = []
    @rows  = []
    @last  = null

  symp: (s)      -> (s.search '{'          ) > 11
  datap: (s)     -> (s.search /@data/i     ) >= 0
  attrp: (s)     -> (s.search /@attribute/i) >= 0
  ignorep: (s)   -> (s.search /\?/i        ) >= 0
  relationp: (s) -> (s.search /@relation/i ) >= 0

  readHeaderString: (s) ->
    @readHeader s, s.split /\s+/g
  readHeader: (s, cells) ->
    if not @data and @datap cells[0]
      @data = true
      if @goals.length is 0
        @last.goalp = true
      for h in @head
        what = if h.goalp then @goals else @ins
        what.push h
    else if @relationp s
      relation = cells[1]
    else if @attrp s
      @n += 1
      c = cells[1]
      unless @ignorep c
        what  = if @symp s then Sym else Num
        @last = new what  c,@n
        @head.push @last
    else
       show "!!!!!????? #{s}"
    @data

  readDataString: (s) ->
    @readData (s.replace /\s+/g,'').split /,/
  readData: (cells) ->
    if cells.length >= @head.length
      prepped = (h.prep cells[h.pos] for h in @head)
      @rows.push new Row prepped,@

  readString : (s) ->
    if (s = s.replace /(%.*|\'|\")/g, '')
      if @data
        @readDataString s
      else
        @data = @readHeaderString s

  main: (lines) ->
    @readString line for line in lines

getFileAsLines '../data/weather.arff',
    (lines) ->
       r = new Reader
       r.main lines
       show r.rows
 
# CREDITS
# =======
# 
# Tim Menzies tim@menzies.us
# 
# INSTALL
# =======
# 
# 0)  Essential first step: make yourself a cup of coffee.
# 1)  Install coffee on your local machine.
# 
# 2)  Download this file and call it, say,  'lib/globals.coffee'.
#     2a) Optional (UNIX systems only)
# 
#        chmod +x lib/globals.coffee  # <=== On UNIX systems
# 
# 3)  Look for 'data/' in this file. Usually, its the last few
#     lines of code (above). If you see any reference to such 
#     a data file, then...
#     3a) Download those data files from
#         http://now.unbox.org/all/trunk/doc/coffee-mine/data
#     3b) Edit this file so it points to those data files.
# 
# 4)  See if this file runs:
# 
#         ./lib/globals.coffee         # <=== UNIX systems
#         coffee lib/globals.coffee    # <=== for other systems
# 
# To check if it is running correctly, see http://coffee.unbox.com 
# (look for blogs entries around lib/getline.coffee).
# 
# COPYRIGHT
# ---------
# 
# Permission is hereby granted, free of charge, to any person obtaining a
# copy of this software and associated documentation files (the "Software"),
# to deal in the Software without restriction, including without limitation
# the rights to use, copy, modify, merge, publish, distribute, sublicense,
# and/or sell copies of the Software, and to permit persons to whom the
# Software is furnished to do so, subject to the following conditions:
# 
# The above copyright notice and this permission notice shall be included
# in all copies or substantial portions of the Software.
# 
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL
# THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR
# OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE,
# ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR
# OTHER DEALINGS IN THE SOFTWARE.
# 
